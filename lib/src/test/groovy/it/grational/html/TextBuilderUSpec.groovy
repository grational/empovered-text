package it.grational.html

import spock.lang.*
import org.jsoup.nodes.Element
import it.grational.text.TextFilter

class TextBuilderUSpec extends Specification {

	@Shared String ls = System.lineSeparator()
	@Shared String strongContent = 'ð¬ð­ð«ð¨ð§ð  ðœð¨ð§ð­ðžð§ð­'
	@Shared String emphasisContent = 'ð‘’ð‘šð‘â„Žð‘Žð‘ ð‘–ð‘  ð‘ð‘œð‘›ð‘¡ð‘’ð‘›ð‘¡'
	@Shared String underlineContent = 'uÌ²nÌ²dÌ²eÌ²rÌ²lÌ²iÌ²nÌ²eÌ² Ì²cÌ²oÌ²nÌ²tÌ²eÌ²nÌ²tÌ²'
	@Shared String emphasisUnderlineContent = 'ð‘’Ì²ð‘šÌ²ð‘Ì²Ì²â„ŽÌ²ð‘ŽÌ²ð‘ Ì²ð‘–Ì²ð‘ Ì² Ì²ð‘¢Ì²ð‘›Ì²ð‘‘Ì²ð‘’Ì²ð‘ŸÌ²ð‘™Ì²ð‘–Ì²ð‘›Ì²ð‘’Ì² Ì²ð‘Ì²ð‘œÌ²ð‘›Ì²ð‘¡Ì²ð‘’Ì²ð‘›Ì²ð‘¡Ì²'
	@Shared String strongUnderlineContent = 'ð¬Ì²ð­Ì²ð«Ì²ð¨Ì²ð§Ì²ð Ì² Ì²ð®Ì²ð§Ì²ðÌ²ðžÌ²ð«Ì²ð¥Ì²ð¢Ì²ð§Ì²ðžÌ² Ì²ðœÌ²ð¨Ì²ð§Ì²ð­Ì²ðžÌ²ð§Ì²ð­Ì²'
	@Shared String strongEmphasisContent = 'ð’”ð’•ð’“ð’ð’ð’ˆ ð’†ð’Žð’‘ð’‰ð’‚ð’”ð’Šð’” ð’„ð’ð’ð’•ð’†ð’ð’•'
	@Shared String strongEmphasisUnderlineContent = 'ð’”Ì²ð’•Ì²ð’“Ì²ð’Ì²ð’Ì²ð’ˆÌ² Ì²ð’†Ì²ð’ŽÌ²ð’‘Ì²ð’‰Ì²ð’‚Ì²ð’”Ì²ð’ŠÌ²ð’”Ì² Ì²ð’–Ì²ð’Ì²ð’…Ì²ð’†Ì²ð’“Ì²ð’Ì²ð’ŠÌ²ð’Ì²ð’†Ì² Ì²ð’„Ì²ð’Ì²ð’Ì²ð’•Ì²ð’†Ì²ð’Ì²ð’•Ì²'

	@Shared TextFilter strongFilter
	@Shared TextFilter emphasisFilter
	@Shared TextFilter underlineFilter
	@Shared TextFilter strongEmphasisFilter

	@Shared TextBuilder textBuilder

	def setupSpec() {
		strongFilter = Stub() {
			filter(emphasisContent) >> strongEmphasisContent
			filter(underlineContent) >> strongUnderlineContent
			filter(_) >> strongContent
		}
		emphasisFilter = Stub() {
			filter(underlineContent) >> emphasisUnderlineContent
			filter(_) >> emphasisContent
		}
		underlineFilter = Stub() {
			filter(_) >> underlineContent
		}
		strongEmphasisFilter = Stub() {
			filter(underlineContent) >> strongEmphasisUnderlineContent
			filter(_) >> strongEmphasisContent
		}

		textBuilder = new TextBuilder (
			strongFilter,
			emphasisFilter,
			underlineFilter,
			strongEmphasisFilter
		)
	}

	@Unroll
	def "Should be capable of altering the text inside the supported html tags"() {
		given:
			Element tag = Mock(Element) {
				nodeName() >> tagName
				text() >> tagContent
			}

		when:
			textBuilder.tail(tag, 0)

		then:
			1 * tag.text(expected)

		where:
			tagName     | tagContent                          || expected
			'strong'    | 'strong content'                    || strongContent
			'em'        | 'emphasis content'                  || emphasisContent
			'u'         | 'underline content'                 || underlineContent
			'emu'       | 'emphasis underline content'        || emphasisUnderlineContent
			'strongu'   | 'strong underline content'          || strongUnderlineContent
			'strongem'  | 'strong emphasis content'           || strongEmphasisContent
			'strongemu' | 'strong emphasis underline content' || strongEmphasisUnderlineContent
	}

	def "Should dump all the text when a root tag is encountered"() {
		given:
			Element tag = Mock(Element) {
				nodeName() >> tagName
				wholeText() >> tagContent
			}

		when:
			textBuilder.tail(tag, 0)

		then:
			textBuilder.toString() == tagContent

		where:
			tagName  | tagContent
			'root'   | '<p><strong>strong content</strong> <em>emphasis content</em> <u>underline content</u></p>'
	}

	def "Should be able to trim and remove multiple empty lines and trailing spaces from the output"() {
		given:
			TextBuilder tb = new TextBuilder (
				strongFilter,
				emphasisFilter,
				underlineFilter,
				strongEmphasisFilter
			)
		and:
			Element tag = Mock(Element) {
				nodeName() >> 'root'
				wholeText() >> '''
				|
				|
				|<ul>
				| <li>
				|  <strong>strong   content</strong>
				|  
				|  
				|  <em>emphasis  content</em>
				|		
				|		
				|  <u>underline content</u>
				| </li>
				|</ul>
				|
				|
				|
				|'''.stripMargin()
			}
		and:
			String expected = '''\
			|<ul>
			| <li>
			| <strong>strong content</strong>
			|
			| <em>emphasis content</em>
			|
			| <u>underline content</u>
			| </li>
			|</ul>'''.stripMargin()

		when:
			tb.tail(tag, 0)

		then:
			tb.toString() == expected
	}

}
